CREATE DATABASE Bitbucket
USE Bitbucket

CREATE TABLE Users
(
	Id INT PRIMARY KEY IDENTITY,
	Username VARCHAR(30) NOT NULL,
	[Password] VARCHAR(30) NOT NULL,
	Email VARCHAR(50) NOT NULL
)

CREATE TABLE Repositories
(
	Id INT PRIMARY KEY IDENTITY,
	[Name] VARCHAR(50) NOT NULL
)

CREATE TABLE RepositoriesContributors
(
	RepositoryId INT FOREIGN KEY REFERENCES Repositories(Id) NOT NULL,
	ContributorId INT FOREIGN KEY REFERENCES Users(Id) NOT NULL,
	CONSTRAINT PK_RepositoriesUsers PRIMARY KEY (RepositoryId, ContributorId)
)

CREATE TABLE Issues
(
	Id INT PRIMARY KEY IDENTITY,
	Title VARCHAR(255) NOT NULL,
	IssueStatus CHAR(6) NOT NULL,
	RepositoryId INT FOREIGN KEY REFERENCES Repositories(Id) NOT NULL,
	AssigneeId INT FOREIGN KEY REFERENCES Users(Id) NOT NULL
)

CREATE TABLE Commits
(
	Id INT PRIMARY KEY IDENTITY,
	[Message] VARCHAR(255) NOT NULL,
	IssueId INT FOREIGN KEY REFERENCES Issues(Id),
	RepositoryId INT FOREIGN KEY REFERENCES Repositories(Id) NOT NULL,
	ContributorId INT FOREIGN KEY REFERENCES Users(Id) NOT NULL
)

CREATE TABLE Files
(
	Id INT PRIMARY KEY IDENTITY,
	[Name] VARCHAR(100) NOT NULL,
	Size DECIMAL(15,2) NOT NULL,
	ParentId INT FOREIGN KEY REFERENCES Files(Id),
	CommitId INT FOREIGN KEY REFERENCES Commits(Id) NOT NULL
)

INSERT INTO Files ([Name], Size, ParentId, CommitId)
VALUES
('Trade.idk', 2598.0, 1, 1),
('menu.net', 9238.31, 2, 2),
('Administrate.soshy', 1246.93, 3, 3),
('Controller.php', 7353.15, 4, 4),
('Find.java', 9957.86, 5, 5),
('Controller.json', 14034.87, 3, 6),
('Operate.xix', 7662.92, 7, 7)

INSERT INTO Issues (Title, IssueStatus, RepositoryId, AssigneeId)
VALUES
('Critical Problem with HomeController.cs file', 'open', 1, 4),
('Typo fix in Judge.html', 'open', 4, 3),
('Implement documentation for UsersService.cs', 'closed', 8, 2),
('Unreachable code in Index.cs', 'open', 9, 8)

UPDATE Issues
SET IssueStatus = 'closed'
WHERE AssigneeId = 6

SELECT Id, [Message], RepositoryId,	ContributorId
FROM Commits
ORDER BY Id ASC, [Message] ASC, RepositoryId ASC, ContributorId ASC

SELECT Id, [Name], Size
FROM Files
WHERE Size > 1000 AND [Name] LIKE '%html'
ORDER BY Size DESC, Id ASC, [Name] ASC 

SELECT i.Id, u.Username + ' : ' + i.Title AS [IssueAssignee]
FROM Issues AS i
JOIN Users AS u ON u.Id = i.AssigneeId
ORDER BY i.Id DESC, i.AssigneeId ASC

SELECT p.Id, p.[Name], CAST (p.Size AS VARCHAR(15))  + 'KB' AS [Size] 
FROM Files AS c
RIGHT JOIN Files AS p ON p.Id = c.ParentId
WHERE c.Id IS NULL
ORDER BY p.Id ASC, p.[Name] ASC, p.Size DESC

SELECT TOP (5) r.Id, r.[Name], COUNT(*) AS [Commits]
FROM RepositoriesContributors AS rc
JOIN Repositories AS r ON r.Id = rc.RepositoryId
JOIN Commits AS c ON c.RepositoryId = r.Id
GROUP BY r.Id, r.[Name]
ORDER BY Commits DESC, r.Id ASC, r.[Name] ASC

SELECT u.Username, AVG(f.Size) AS [Size]
FROM Users AS u
JOIN Commits AS c ON c.ContributorId = u.Id
JOIN Files AS f ON f.CommitId = c.Id
GROUP BY u.Username
ORDER BY AVG(f.Size) DESC, u.Username ASC

GO
CREATE FUNCTION udf_AllUserCommits(@username VARCHAR(30)) 
RETURNS INT
BEGIN
	DECLARE @CommitCount INT = 
		(SELECT COUNT(c.Id)
		FROM Commits AS c
		JOIN Users AS u ON u.Id = c.ContributorId
		WHERE u.Username = @username);
	RETURN @CommitCount;
END
GO

SELECT dbo.udf_AllUserCommits('UnderSinduxrein')
GO

CREATE OR ALTER PROCEDURE usp_SearchForFiles(@fileExtension VARCHAR(20))
AS
	SELECT Id, [Name], CAST(Size AS VARCHAR(15))  + 'KB' AS [Size] 
	FROM Files
	WHERE [Name] LIKE '%' + @fileExtension
	ORDER BY Id ASC, [Name] ASC, Size DESC
GO

EXEC usp_SearchForFiles 'txt'

DELETE RepositoriesContributors
FROM RepositoriesContributors AS rc
LEFT JOIN Repositories AS r ON rc.RepositoryId = r.Id
WHERE r.[Name] = 'Softuni-Teamwork'

DELETE Issues
FROM Issues AS i
LEFT JOIN Repositories AS r ON r.Id = i.RepositoryId
WHERE r.[Name] = 'Softuni-Teamwork'
